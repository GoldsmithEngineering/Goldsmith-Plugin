;;; SuperQuickSave.LSP -- Saves autoCAD drawing with everything but the 0 layer frozen and everything (including regaps) purged.
;;
;;; Copyright (C) 2014, Creative Commons License.
;;;
;;; Author: Szabolcs Pasztor <spasztor@goldsmithengineering.com>
;;; Created: 27 August 2014
;;; Version: 1.0.1
;;; Keywords: sss, super, quick, save
;;;
;;; Commentary: This routine first purges everything from the drawing then 
;;;				continues to purge regapps. Then it saves the current layer 
;;;				state under [FILENAME]-temp (deletes the old temp layer under
;;;				that name if it exists) and then sets the current layer to "0".
;;;				Finally, it freezes all the layers but the current, saves the
;;;				drawing and then restores the saved layer state.
;;;
;;; Revisions:
;;; 	(09-30-2014, Szabolcs Pasztor) - Added error handling and removed saving layerstate SQS-TEMP.
;;;
;;; Code:
(defun c:SSS ( / *error* nom layerstatename)

  	;; Error Handling:
        (defun *error* ( msg )

	    (if nom (setvar 'nomutt nom))

            (command "layer" "state" "restore" layerstatename "" "" "")
	    (command "layer" "state" "delete" "SQS-Temp" "" "" "")
	    (command "regenall" "")

	    (if (not (member msg '("Function cancelled" "quit / exit abort")))
	        (princ (strcat "\nError: " msg))
	    )
	    (princ)
	)

	;; Set NOMUTT system variable to 1 and save old state.
	(setq nom (getvar 'nomutt))
	(setvar 'nomutt 1)

	;; Purge file:
	(command "purge" "all" "*" "no")
	(command "purge" "regapp" "*" "no")
	
	;; Save Current Layer State:
	(setq layerstatename (strcat (vl-filename-base (getvar "dwgname")) "-temp"))
	;; Required to delete layerstatename (cannon delete current layer state)
	(command "layer" "state" "save" "SQS-Temp" "" "" "")
	(command "layer" "state" "restore" "SQS-Temp" "" "" "")
	(if (layerstate-has layerstatename)
		(command "layer" "state" "delete" layerstatename "" "" "")
	)
	(command "layer" "state" "save" layerstatename "" "" "")
	
	;; Freeze all layers but "0" layer and save:
	(command "layer" "set" "0" "")
	(command "layer" "freeze" "*" "")
	(command "qsave")
	
	;; Restore old layer state and regen drawing.
	(command "layer" "state" "restore" layerstatename "" "")
	(command "layer" "state" "delete" "SQS-Temp" "" "" "")
	(command "regenall" "")

	;; Restore NOMUTT system variable and *error* symbol.
	(setvar 'nomutt 0)
	(setq *error* nil)

	(print "Super Quick Save Complete.")
)
;;; SuperQuickSave.LSP
